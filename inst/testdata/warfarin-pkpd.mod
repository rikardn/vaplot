$PROB WARFARIN PKPD

$DATA warfarin-sim IGNORE=@

$INPUT ID WT AGE SEX TIME AMT RATX DVID DV MDV EVID 


$THETA
(0.01,0.134,1) ; POPCL L/h/70kg
(0.01,8.09,20) ; POPV L/70kg
(0.01, 0.6,24) ; POPKA h
(0.01,96.5,200) ; POPE0
(-INF,-74.3,0) ; POPEMAX
(0.01,0.219,100) ; POPC50

$OMEGA
0.09 ; PPVCL
0.09 ; PPVV
0.9 ; PPVKA
0.001 ; PPVE0
0.01 ; PPVEMAX
0.5 ; PPVC50

$SIGMA 0.00697 ; RUVCV
$SIGMA 0.0824 ; RUVSD mg/L
$SIGMA 30 ; RUVFX

$SUBR ADVAN2 TRAN2

$PK


   FSZV=WT/70
   FSZCL=FSZV**0.75

   CL=FSZCL*THETA(1)*EXP(ETA(1))
   V=FSZV*THETA(2)*EXP(ETA(2))
    KA=THETA(3)*EXP(ETA(3))



   S2=V

   E0=THETA(4)*EXP(ETA(4))
   EMAX=THETA(5)*EXP(ETA(5))
   C50=(1-SEX*0.5)*THETA(6)*EXP(ETA(6))


$ERROR

   CP=F
   PCA=E0 + EMAX*CP/(C50+CP)
   IF (DVID.LE.1) THEN
    IPRED = CP
      Y=CP*(1+ERR(1)) + ERR(2)
   ENDIF
   IF (DVID.EQ.2) THEN
   IPRED = PCA
      Y=PCA + ERR(3)
   ENDIF


$ESTIMATION METHOD=COND INTERACTION PRINT=1 NOABORT 
$TABLE  ID TIME WT AGE SEX AMT RATX DVID 
DV MDV EVID  NOAPPEND ONEHEADER NOPRINT  FILE=sdtab